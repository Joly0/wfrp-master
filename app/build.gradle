apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "androidx.navigation.safeargs.kotlin"

def playStoreJsonFile = file('./play_store_credentials.json')

if (playStoreJsonFile.isFile()) {
    apply plugin: 'com.github.triplet.play'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "cz.frantisekmasa.dnd"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName System.getenv("APP_VERSION_NAME") ?: "dev"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //
        // Firestore emulator setup
        //
        Properties properties = new Properties()
        File propertiesFile = project.rootProject.file("local.properties");

        if (propertiesFile.exists()) {
            properties.load(propertiesFile.newDataInputStream())
        }

        buildConfigField 'String', 'FIRESTORE_EMULATOR_URL', '"' + properties.getOrDefault('dev.firestoreEmulatorUrl', '') + '"'
        //
        // End of Firestore Emulator setup
        //
    }

    signingConfigs {
        release {
            storeFile file(".keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias "uploadKey"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "[Debug] WFRP Master"
            resValue "string", "character_ad_unit_id", "ca-app-pub-3940256099942544/6300978111"
        }

        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "WFRP Master"
            resValue "string", "character_ad_unit_id", "ca-app-pub-8647604386686373/9919978313"
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}


if (playStoreJsonFile.exists()) {
    play {
        defaultToAppBundles = true
        track = System.getenv("PLAY_STORE_TRACK")
        serviceAccountCredentials = playStoreJsonFile
        resolutionStrategy = "auto"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0'
    implementation "org.koin:koin-android:2.1.5"
    implementation "org.koin:koin-android-viewmodel:2.1.5"
    implementation "org.koin:koin-androidx-fragment:2.1.5"

    // Firebase-related dependencies
    implementation 'com.google.firebase:firebase-analytics:17.4.2'
    implementation 'com.firebaseui:firebase-ui-auth:6.2.0'
    implementation 'com.google.firebase:firebase-firestore-ktx:21.4.3'
    implementation 'com.google.firebase:firebase-analytics-ktx:17.4.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'

    implementation 'com.getbase:floatingactionbutton:1.10.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.5"

    implementation 'com.google.android.gms:play-services-ads:19.1.0'
    implementation 'com.google.android.ads.consent:consent-library:1.0.8'

    implementation "com.google.zxing:core:3.4.0"
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "io.arrow-kt:arrow-core:0.10.4"
}
